plugins {
    id "com.jfrog.bintray" version "1.7.3"
}

ext {
    libraryVersion = "0.3.3"
    libraryName = 'datetimepicker'
    libraryArtifact = 'datetimepicker'
    libraryDesc = "Date time picker for android"
    bintrayGroupId = 'com.gavincode'

    siteUrl = "https://github.com/gavinlin/DateTimePicker.git"
    gitUrl = "https://github.com/gavinlin/DateTimePicker.git"

    developerId = "gavin"
    developerName = "gavin"
    developerEmail = "gavinlin.au@gmail.com"

    licenseName = 'The Apache Software License, Version 2.0'
    licenseUrl = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
    allLicenses = ["Apache-2.0"]
}

apply plugin: 'com.android.library'
apply plugin: 'maven-publish'

group = bintrayGroupId
version = libraryVersion

android {
    compileSdkVersion 25
    buildToolsVersion "26.0.0"


    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 3
        versionName "3.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    implementation 'com.android.support:appcompat-v7:25.4.0'
    compile 'com.android.support:support-v4:25.4.0'
    compile 'com.jakewharton.threetenabp:threetenabp:1.0.5'
    testImplementation 'junit:junit:4.12'
}


// Create the pom configuration:
def pomConfig = {
    licenses {
        license {
            name licenseName
            url licenseUrl
            distribution "repo"
        }
    }
    developers {
        developer {
            id developerId
            name developerName
            email developerEmail
        }
    }
}

task generateSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives generateSourcesJar
    archives javadocJar
}

bintray {
    user = bintrayUser
    key = bintrayApiKey
    configurations = ['archives']
    publications = ['DateTimePicker']

    pkg {
        repo = 'android'
        name = libraryName
        userOrg = 'gavinlinau'
        licenses = allLicenses
        vcsUrl = gitUrl

        version {
            name = libraryVersion
            desc = libraryDesc
            released = new Date()
            vcsTag = libraryVersion
        }
    }
}

publishing {
    publications {
        DateTimePicker(MavenPublication) {
            groupId bintrayGroupId
            artifactId libraryArtifact
            version libraryVersion

            pom.withXml {
                def root = asNode()
                root.appendNode('name', libraryName)
                root.appendNode('description', libraryDesc)
                root.appendNode('url', siteUrl)
                root.children().last() + pomConfig
                def dependenciesNode = root.appendNode('dependencies')

                configurations.compile.allDependencies.each { dependency ->
                    def dependencyNode = dependenciesNode.appendNode('dependency')
                    dependencyNode.appendNode('groupId', dependency.group)
                    dependencyNode.appendNode('artifactId', dependency.name)
                    dependencyNode.appendNode('version', dependency.version)
                }
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
            }
        }
    }
}


task publishDateTimePickerBintray(dependsOn:[':datetimepicker:assemble',':datetimepicker:generatePomFileForDateTimePickerPublication',':datetimepicker:bintrayUpload']) {
    tasks.findByPath(':datetimepicker:assemble').mustRunAfter ':datetimepicker:clean'
    tasks.findByPath(':datetimepicker:generatePomFileForDateTimePickerPublication').mustRunAfter ':datetimepicker:assemble'
    tasks.findByPath(':datetimepicker:bintrayUpload').mustRunAfter ':datetimepicker:generatePomFileForDateTimePickerPublication'
}

configure(publishDateTimePickerBintray) {
    group = 'bintray'
}